diff --git a/CMakeLists.txt b/CMakeLists.txt
new file mode 100644
index 00000000..bb97bb11
--- /dev/null
+++ b/CMakeLists.txt
@@ -0,0 +1,97 @@
+cmake_minimum_required(VERSION 3.15)
+list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
+
+# option() honors normal variables.
+if(POLICY CMP0077)
+  cmake_policy(SET CMP0077 NEW)
+endif()
+
+# MSVC runtime library flags are selected by an abstraction.
+if(POLICY CMP0091)
+  cmake_policy(SET CMP0091 NEW)
+endif()
+
+include(ParseAc)
+parse_ac(VERSION MAJOR MINOR PATCH)
+
+project(CoinUtils VERSION ${VERSION})
+
+# config options
+if(NOT CMAKE_BUILD_TYPE)
+  set(CMAKE_BUILD_TYPE "Release" CACHE STRING
+    "Choose the type of build, options are: Debug Release RelWithDebInfo MinSizeRel."
+    FORCE)
+endif(NOT CMAKE_BUILD_TYPE)
+
+option(BUILD_SHARED_LIBS "" ON)
+if(BUILD_SHARED_LIBS AND MSVC)
+  set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
+endif(BUILD_SHARED_LIBS AND MSVC)
+
+# config options
+if(MSVC)
+  # Build with multiple processes
+  add_definitions(/MP)
+  add_definitions(/D_CRT_SECURE_NO_WARNINGS /D_CRT_SECURE_NO_DEPRECATE)
+  # MSVC warning suppressions
+  add_definitions(
+    /wd4018 # 'expression' : signed/unsigned mismatch
+    /wd4065 # switch statement contains 'default' but no 'case' labels
+    /wd4101 # 'identifier' : unreferenced local variable
+    /wd4102 # 'label' : unreferenced label
+    /wd4244 # 'conversion' conversion from 'type1' to 'type2', possible loss of data
+    /wd4267 # 'var' : conversion from 'size_t' to 'type', possible loss of data
+    /wd4309 # 'conversion' : truncation of constant value
+    /wd4805 # 'operation' : unsafe mix of type 'type1' and type 'type2' in operation.
+    /wd4996 # The compiler encountered a deprecated declaration.
+    )
+endif()
+if(APPLE)
+  set(
+    CMAKE_CXX_FLAGS
+    "${CMAKE_CXX_FLAGS} -Wno-inconsistent-missing-override -Wno-unused-command-line-argument -Wno-unused-result -Wno-exceptions"
+    )
+  set(CMAKE_OSX_DEPLOYMENT_TARGET "10.9" CACHE STRING "Minimum OS X deployment version")
+endif(APPLE)
+
+# ZLIB
+if(NOT TARGET ZLIB::ZLIB)
+  find_package(ZLIB)
+endif()
+if(ZLIB_FOUND OR TARGET ZLIB::ZLIB)
+  message(STATUS "Use zlib")
+  set(HAVE_ZLIB_H "1" CACHE INTERNAL "Use zlib")
+  set(COIN_HAS_ZLIB "1" CACHE INTERNAL "Use zlib")
+endif()
+
+# PThread
+set(THREADS_PREFER_PTHREAD_FLAG ON)
+find_package(Threads)
+if(CMAKE_USE_PTHREADS_INIT)
+  set(PTHREADS_FOUND TRUE)
+else()
+  set(PTHREADS_FOUND FALSE)
+endif()
+
+include(CheckEnv)
+include(CTest)
+
+add_subdirectory(CoinUtils)
+
+include(GNUInstallDirs)
+install(EXPORT ${PROJECT_NAME}Targets
+  NAMESPACE Coin::
+  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")
+include(CMakePackageConfigHelpers)
+configure_package_config_file(cmake/Config.cmake.in
+  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
+  INSTALL_DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")
+write_basic_package_version_file(
+  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
+  COMPATIBILITY SameMajorVersion)
+install(
+  FILES
+  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
+  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
+  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}"
+  COMPONENT Devel)
\ No newline at end of file
diff --git a/CoinUtils/CMakeLists.txt b/CoinUtils/CMakeLists.txt
new file mode 100644
index 00000000..d00633d7
--- /dev/null
+++ b/CoinUtils/CMakeLists.txt
@@ -0,0 +1,170 @@
+set(NAME "COINUTILS")
+
+# PTHREAD
+if(PTHREADS_FOUND)
+  set(${NAME}_PTHREADS "1" CACHE INTERNAL "Use pthread")
+endif()
+
+set(COIN_${NAME}_CHECKLEVEL "0" CACHE INTERNAL
+  "${NAME} check level")
+set(COIN_${NAME}_VERBOSITY "0" CACHE INTERNAL
+  "${NAME} verbosity level")
+configure_file(config.h.cmake.in config.h)
+configure_file(config_coinutils.h.cmake.in config_coinutils.h)
+
+set(_SRCS
+  src/CoinAlloc.cpp
+  src/CoinBuild.cpp
+  src/CoinDenseVector.cpp
+  src/CoinError.cpp
+  src/CoinFactorization1.cpp
+  src/CoinFactorization2.cpp
+  src/CoinFactorization3.cpp
+  src/CoinFactorization4.cpp
+  src/CoinSimpFactorization.cpp
+  src/CoinDenseFactorization.cpp
+  src/CoinOslFactorization.cpp
+  src/CoinOslFactorization2.cpp
+  src/CoinOslFactorization3.cpp
+  src/CoinFileIO.cpp
+  src/CoinFinite.cpp
+  src/CoinIndexedVector.cpp
+  src/CoinLpIO.cpp
+  src/CoinMessage.cpp
+  src/CoinMessageHandler.cpp
+  src/CoinModel.cpp
+  src/CoinStructuredModel.cpp
+  src/CoinModelUseful.cpp
+  src/CoinModelUseful2.cpp
+  src/CoinMpsIO.cpp
+  src/CoinPackedMatrix.cpp
+  src/CoinPackedVector.cpp
+  src/CoinPackedVectorBase.cpp
+  src/CoinParam.cpp
+  src/CoinParamUtils.cpp
+  src/CoinPostsolveMatrix.cpp
+  src/CoinPrePostsolveMatrix.cpp
+  src/CoinPresolveDoubleton.cpp
+  src/CoinPresolveDual.cpp
+  src/CoinPresolveDupcol.cpp
+  src/CoinPresolveEmpty.cpp
+  src/CoinPresolveFixed.cpp
+  src/CoinPresolveForcing.cpp
+  src/CoinPresolveHelperFunctions.cpp
+  src/CoinPresolveImpliedFree.cpp
+  src/CoinPresolveIsolated.cpp
+  src/CoinPresolveMatrix.cpp
+  src/CoinPresolvePsdebug.cpp
+  src/CoinPresolveMonitor.cpp
+  src/CoinPresolveSingleton.cpp
+  src/CoinPresolveSubst.cpp
+  src/CoinPresolveTighten.cpp
+  src/CoinPresolveTripleton.cpp
+  src/CoinPresolveUseless.cpp
+  src/CoinPresolveZeros.cpp
+  src/CoinSearchTree.cpp
+  src/CoinShallowPackedVector.cpp
+  src/CoinSnapshot.cpp
+  src/CoinWarmStartBasis.cpp
+  src/CoinWarmStartVector.cpp
+  src/CoinWarmStartDual.cpp
+  src/CoinWarmStartPrimalDual.cpp
+  src/CoinRational.cpp)
+
+set(_HDRS
+  src/Coin_C_defines.h
+  src/CoinUtilsConfig.h
+  src/CoinAlloc.hpp
+  src/CoinBuild.hpp
+  src/CoinDenseVector.hpp
+  src/CoinDistance.hpp
+  src/CoinError.hpp
+  src/CoinFactorization.hpp
+  src/CoinSimpFactorization.hpp
+  src/CoinDenseFactorization.hpp
+  src/CoinOslFactorization.hpp
+  src/CoinFileIO.hpp
+  src/CoinFinite.hpp
+  src/CoinFloatEqual.hpp
+  src/CoinHelperFunctions.hpp
+  src/CoinIndexedVector.hpp
+  src/CoinLpIO.hpp
+  src/CoinMessage.hpp
+  src/CoinMessageHandler.hpp
+  src/CoinModel.hpp
+  src/CoinStructuredModel.hpp
+  src/CoinModelUseful.hpp
+  src/CoinMpsIO.hpp
+  src/CoinPackedMatrix.hpp
+  src/CoinPackedVector.hpp
+  src/CoinPackedVectorBase.hpp
+  src/CoinParam.hpp
+  src/CoinPragma.hpp
+  src/CoinPresolveDoubleton.hpp
+  src/CoinPresolveDual.hpp
+  src/CoinPresolveDupcol.hpp
+  src/CoinPresolveEmpty.hpp
+  src/CoinPresolveFixed.hpp
+  src/CoinPresolveForcing.hpp
+  src/CoinPresolveImpliedFree.hpp
+  src/CoinPresolveIsolated.hpp
+  src/CoinPresolveMatrix.hpp
+  src/CoinPresolveMonitor.hpp
+  src/CoinPresolvePsdebug.hpp
+  src/CoinPresolveSingleton.hpp
+  src/CoinPresolveSubst.hpp
+  src/CoinPresolveTighten.hpp
+  src/CoinPresolveTripleton.hpp
+  src/CoinPresolveUseless.hpp
+  src/CoinPresolveZeros.hpp
+  src/CoinSearchTree.hpp
+  src/CoinShallowPackedVector.hpp
+  src/CoinSignal.hpp
+  src/CoinSmartPtr.hpp
+  src/CoinSnapshot.hpp
+  src/CoinSort.hpp
+  src/CoinTime.hpp
+  src/CoinTypes.hpp
+  src/CoinUtility.hpp
+  src/CoinWarmStart.hpp
+  src/CoinWarmStartBasis.hpp
+  src/CoinWarmStartVector.hpp
+  src/CoinWarmStartDual.hpp
+  src/CoinWarmStartPrimalDual.hpp
+  src/CoinRational.hpp)
+
+add_library(CoinUtils ${_SRCS} ${_HDRS})
+target_include_directories(CoinUtils PUBLIC
+  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
+  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
+  $<INSTALL_INTERFACE:include/coin>)
+target_compile_definitions(CoinUtils
+  PUBLIC HAVE_CONFIG_H
+  PRIVATE COINUTILS_BUILD)
+if(CMAKE_VERSION VERSION_LESS "3.8.2")
+  set_property(TARGET CoinUtils PROPERTY CXX_STANDARD 11)
+  set_property(TARGET CoinUtils PROPERTY CXX_STANDARD_REQUIRED ON)
+else()
+  target_compile_features(CoinUtils PUBLIC cxx_std_11)
+endif()
+if(TARGET ZLIB::ZLIB)
+  target_link_libraries(CoinUtils PRIVATE	ZLIB::ZLIB)
+endif()
+if(CMAKE_USE_PTHREADS_INIT)
+  target_link_libraries(CoinUtils PRIVATE	Threads::Threads)
+endif()
+set_target_properties(CoinUtils PROPERTIES
+  PUBLIC_HEADER "${_HDRS};${CMAKE_CURRENT_BINARY_DIR}/config_coinutils.h"
+  VERSION ${PROJECT_VERSION}
+  SOVERSION ${PROJECT_VERSION_MAJOR})
+add_library(Coin::CoinUtils ALIAS CoinUtils)
+
+# Install
+include(GNUInstallDirs)
+install(TARGETS CoinUtils
+  EXPORT ${PROJECT_NAME}Targets
+  PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/coin
+  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  )
+ 
\ No newline at end of file
diff --git a/CoinUtils/config.h.cmake.in b/CoinUtils/config.h.cmake.in
new file mode 100644
index 00000000..dd9b4f3e
--- /dev/null
+++ b/CoinUtils/config.h.cmake.in
@@ -0,0 +1,126 @@
+/*config.h. Generated by configure_file.*/
+
+/* VERSION */
+#define VERSION "${VERSION}"
+/* ${NAME}_VERSION */
+#define ${NAME}_VERSION "${VERSION}"
+/* ${NAME}_VERSION_MAJOR */
+#define ${NAME}_VERSION_MAJOR ${MAJOR}
+/* ${NAME}_VERSION_MINOR */
+#define ${NAME}_VERSION_MINOR ${MINOR}
+/* ${NAME}_VERSION_RELEASE */
+#define ${NAME}_VERSION_RELEASE ${PATCH}
+
+/* HAVE_MATH_H */
+#cmakedefine HAVE_MATH_H ${HAVE_MATH_H}
+/* HAVE_CTYPE_H */
+#cmakedefine HAVE_CTYPE_H ${HAVE_CTYPE_H}
+/* HAVE_INTTYPES_H */
+#cmakedefine HAVE_INTTYPES_H ${HAVE_INTTYPES_H}
+/* HAVE_FLOAT_H */
+#cmakedefine HAVE_FLOAT_H ${HAVE_FLOAT_H}
+/* HAVE_IEEEFP_H */
+#cmakedefine HAVE_IEEEFP_H ${HAVE_IEEEFP_H}
+/* HAVE_STDARG_H */
+#cmakedefine HAVE_STDARG_H ${HAVE_STDARG_H}
+/* HAVE_STDDEF_H */
+#cmakedefine HAVE_STDDEF_H ${HAVE_STDDEF_H}
+/* HAVE_STDINT_H */
+#cmakedefine HAVE_STDINT_H ${HAVE_STDINT_H}
+/* HAVE_STDIO_H */
+#cmakedefine HAVE_STDIO_H ${HAVE_STDIO_H}
+/* HAVE_STDLIB_H */
+#cmakedefine HAVE_STDLIB_H ${HAVE_STDLIB_H}
+/* HAVE_ASSERT_H */
+#cmakedefine HAVE_ASSERT_H ${HAVE_ASSERT_H}
+/* HAVE_DLFCN_H */
+#cmakedefine HAVE_DLFCN_H ${HAVE_DLFCN_H}
+/* HAVE_ENDIAN_H */
+#cmakedefine HAVE_ENDIAN_H ${HAVE_ENDIAN_H}
+/* HAVE_MEMORY_H */
+#cmakedefine HAVE_MEMORY_H ${HAVE_MEMORY_H}
+/* HAVE_STRINGS_H */
+#cmakedefine HAVE_STRINGS_H ${HAVE_STRINGS_H}
+/* HAVE_STRING_H */
+#cmakedefine HAVE_STRING_H ${HAVE_STRING_H}
+/* HAVE_TIME_H */
+#cmakedefine HAVE_TIME_H ${HAVE_TIME_H}
+/* HAVE_UNISTD_H */
+#cmakedefine HAVE_UNISTD_H ${HAVE_UNISTD_H}
+/* HAVE_SYS_STAT_H */
+#cmakedefine HAVE_SYS_STAT_H ${HAVE_SYS_STAT_H}
+/* HAVE_SYS_TYPES_H */
+#cmakedefine HAVE_SYS_TYPES_H ${HAVE_SYS_TYPES_H}
+
+/* HAVE_CMATH */
+#cmakedefine HAVE_CMATH ${HAVE_CMATH}
+/* HAVE_CCTYPE */
+#cmakedefine HAVE_CCTYPE ${HAVE_CCTYPE}
+/* HAVE_CINTTYPES */
+#cmakedefine HAVE_CINTTYPES ${HAVE_CINTTYPES}
+/* HAVE_CFLOAT */
+#cmakedefine HAVE_CFLOAT ${HAVE_CFLOAT}
+/* HAVE_CIEEEFP */
+#cmakedefine HAVE_CIEEEFP ${HAVE_CIEEEFP}
+/* HAVE_CSTDARG */
+#cmakedefine HAVE_CSTDARG ${HAVE_CSTDARG}
+/* HAVE_CSTDDEF */
+#cmakedefine HAVE_CSTDDEF ${HAVE_CSTDDEF}
+/* HAVE_CSTDINT */
+#cmakedefine HAVE_CSTDINT ${HAVE_CSTDINT}
+/* HAVE_CSTDIO */
+#cmakedefine HAVE_CSTDIO ${HAVE_CSTDIO}
+/* HAVE_CSTDLIB */
+#cmakedefine HAVE_CSTDLIB ${HAVE_CSTDLIB}
+/* HAVE_CASSERT */
+#cmakedefine HAVE_CASSERT ${HAVE_CASSERT}
+/* HAVE_CSTRING */
+#cmakedefine HAVE_CSTRING ${HAVE_CSTRING}
+/* HAVE_CTIME */
+#cmakedefine HAVE_CTIME ${HAVE_CTIME}
+
+/* STDC_HEADERS */
+#cmakedefine STDC_HEADERS ${STDC_HEADERS}
+/* COIN_C_FINITE */
+#cmakedefine COIN_C_FINITE ${COIN_C_FINITE}
+/* COIN_C_ISNAN */
+#cmakedefine COIN_C_ISNAN ${COIN_C_ISNAN}
+/* COIN_INT64_T */
+#cmakedefine COIN_INT64_T ${COIN_INT64_T}
+/* COIN_UINT64_T */
+#cmakedefine COIN_UINT64_T ${COIN_UINT64_T}
+/* COIN_INTPTR_T */
+#cmakedefine COIN_INTPTR_T ${COIN_INTPTR_T}
+
+/* COIN_${NAME}_CHECKLEVEL */
+#define COIN_${NAME}_CHECKLEVEL ${COIN_${NAME}_CHECKLEVEL}
+/* COIN_${NAME}_VERBOSITY */
+#define COIN_${NAME}_VERBOSITY ${COIN_${NAME}_VERBOSITY}
+
+/* ${NAME}_HAS_CSTDINT */
+#ifdef HAVE_CSTDINT
+#define ${NAME}_HAS_CSTDINT 1
+#endif
+
+/* ${NAME}_PTHREADS */
+#cmakedefine COINUTILS_PTHREADS ${COINUTILS_PTHREADS}
+/* ${NAME}_MEMPOOL_MAXPOOLED */
+#define ${NAME}_MEMPOOL_MAXPOOLED -1
+
+/* HAVE_ZLIB_H */
+#cmakedefine HAVE_ZLIB_H ${HAVE_ZLIB_H}
+/* COIN_HAS_ZLIB */
+#cmakedefine COIN_HAS_ZLIB ${COIN_HAS_ZLIB}
+
+/* PACKAGE */
+#cmakedefine PACKAGE
+/* PACKAGE_NAME */
+#cmakedefine PACKAGE_NAME
+/* PACKAGE_VERSION */
+#cmakedefine PACKAGE_VERSION
+/* PACKAGE_STRING */
+#cmakedefine PACKAGE_STRING
+/* PACKAGE_TARNAME */
+#cmakedefine PACKAGE_TARNAME
+/* PACKAGE_BUGREPORT */
+#cmakedefine PACKAGE_BUGREPORT
\ No newline at end of file
diff --git a/CoinUtils/config_coinutils.h.cmake.in b/CoinUtils/config_coinutils.h.cmake.in
new file mode 100644
index 00000000..b12c17be
--- /dev/null
+++ b/CoinUtils/config_coinutils.h.cmake.in
@@ -0,0 +1,22 @@
+#ifndef __CONFIG_${NAME}_H__
+#define __CONFIG_${NAME}_H__
+
+#define ${NAME}_HAS_CSTDINT 1
+
+/* ${NAME}_VERSION */
+#define ${NAME}_VERSION "${VERSION}"
+/* ${NAME}_VERSION_MAJOR */
+#define ${NAME}_VERSION_MAJOR ${MAJOR}
+/* ${NAME}_VERSION_MINOR */
+#define ${NAME}_VERSION_MINOR ${MINOR}
+/* ${NAME}_VERSION_RELEASE */
+#define ${NAME}_VERSION_RELEASE ${PATCH}
+
+/* COIN_INT64_T */
+#cmakedefine COIN_INT64_T ${COIN_INT64_T}
+/* COIN_UINT64_T */
+#cmakedefine COIN_UINT64_T ${COIN_UINT64_T}
+/* COIN_INTPTR_T */
+#cmakedefine COIN_INTPTR_T ${COIN_INTPTR_T}
+
+#endif
\ No newline at end of file
diff --git a/CoinUtils/src/CoinError.cpp b/CoinUtils/src/CoinError.cpp
index ff79264d..d61623a0 100644
--- a/CoinUtils/src/CoinError.cpp
+++ b/CoinUtils/src/CoinError.cpp
@@ -5,7 +5,7 @@
 
 #include "CoinError.hpp"
 
-bool CoinError::printErrors_ = false;
+//bool CoinError::printErrors_ = false;
 
 /** A function to block the popup windows that windows creates when the code
     crashes */
diff --git a/CoinUtils/src/CoinError.hpp b/CoinUtils/src/CoinError.hpp
index 909baacd..fb98c33f 100644
--- a/CoinUtils/src/CoinError.hpp
+++ b/CoinUtils/src/CoinError.hpp
@@ -115,7 +115,7 @@ public:
     , file_(fileName_)
     , lineNumber_(line)
   {
-    print(printErrors_);
+    print(/*printErrors_*/);
   }
 
   /// Copy constructor
@@ -166,7 +166,7 @@ private:
 
 public:
   /// Whether to print every error
-  static bool printErrors_;
+  //static bool printErrors_;
 };
 
 #ifndef __STRING
diff --git a/cmake/CheckEnv.cmake b/cmake/CheckEnv.cmake
new file mode 100644
index 00000000..b2477025
--- /dev/null
+++ b/cmake/CheckEnv.cmake
@@ -0,0 +1,151 @@
+# Flags
+include (CheckIncludeFile)
+check_include_file(math.h HAVE_MATH_H)
+check_include_file(ctype.h HAVE_CTYPE_H)
+check_include_file(inttypes.h HAVE_INTTYPES_H)
+check_include_file(float.h HAVE_FLOAT_H)
+check_include_file(ieeefp.h HAVE_IEEEFP_H)
+check_include_file(stdarg.h HAVE_STDARG_H)
+check_include_file(stddef.h HAVE_STDDEF_H)
+check_include_file(stdint.h HAVE_STDINT_H)
+check_include_file(stdio.h HAVE_STDIO_H)
+check_include_file(stdlib.h HAVE_STDLIB_H)
+check_include_file(assert.h HAVE_ASSERT_H)
+check_include_file(dlfcn.h HAVE_DLFCN_H)
+check_include_file(endian.h HAVE_ENDIAN_H)
+check_include_file(memory.h HAVE_MEMORY_H)
+check_include_file(strings.h HAVE_STRINGS_H)
+check_include_file(string.h HAVE_STRING_H)
+check_include_file(time.h HAVE_TIME_H)
+check_include_file(unistd.h HAVE_UNISTD_H)
+check_include_file(sys/stat.h HAVE_SYS_STAT_H)
+check_include_file(sys/types.h HAVE_SYS_TYPES_H)
+
+include (CheckIncludeFileCXX)
+check_include_file_cxx(cmath HAVE_CMATH)
+check_include_file_cxx(cctype HAVE_CCTYPE)
+check_include_file_cxx(cinttypes HAVE_CINTTYPES)
+check_include_file_cxx(cfloat HAVE_CFLOAT)
+check_include_file_cxx(cieeefp HAVE_CIEEEFP)
+check_include_file_cxx(cstdarg HAVE_CSTDARG)
+check_include_file_cxx(cstddef HAVE_CSTDDEF)
+check_include_file_cxx(cstdint HAVE_CSTDINT)
+check_include_file_cxx(cstdio HAVE_CSTDIO)
+check_include_file_cxx(cstdlib HAVE_CSTDLIB)
+check_include_file_cxx(cassert HAVE_CASSERT)
+check_include_file_cxx(cstring HAVE_CSTRING)
+check_include_file_cxx(ctime HAVE_CTIME)
+
+set(STDC_HEADERS 1 CACHE INTERNAL "System has ANSI C header files")
+
+set(TEST_INCLUDES "")
+if(HAVE_CMATH)
+  list(APPEND TEST_INCLUDES "cmath")
+endif()
+if(HAVE_CFLOAT)
+  list(APPEND TEST_INCLUDES "cfloat")
+endif()
+if(HAVE_CIEEEFP)
+  list(APPEND TEST_INCLUDES "cieeefp")
+endif()
+if(HAVE_MATH_H)
+  list(APPEND TEST_INCLUDES "math.h")
+endif()
+if(HAVE_FLOAT_H)
+  list(APPEND TEST_INCLUDES "float.h")
+endif()
+if(HAVE_IEEEFP_H)
+  list(APPEND TEST_INCLUDES "ieeefp.h")
+endif()
+
+# ISFINITE
+include(CheckCXXSourceCompiles)
+check_cxx_source_compiles(
+  "#include <cmath>\nint main(){return std::isfinite(0);}"
+  HAVE_STD_ISFINITE)
+include(CheckFunctionExists)
+include(CheckCXXSymbolExists)
+check_cxx_symbol_exists(isfinite "${TEST_INCLUDES}" HAVE_ISFINITE)
+check_cxx_symbol_exists(finite   "${TEST_INCLUDES}" HAVE_FINITE)
+check_cxx_symbol_exists(_finite  "${TEST_INCLUDES}" HAVE__FINITE)
+check_cxx_symbol_exists(__finite "${TEST_INCLUDES}" HAVE___FINITE)
+if(HAVE_STD_ISFINITE)
+  set(COIN_C_FINITE "std::isfinite")
+elseif(HAVE_ISFINITE)
+  set(COIN_C_FINITE "isfinite")
+elseif(HAVE_FINITE)
+  set(COIN_C_FINITE "finite")
+elseif(HAVE__FINITE)
+  set(COIN_C_FINITE "_finite")
+elseif(HAVE___FINITE)
+  set(COIN_C_FINITE "__finite")
+else()
+  message(FATAL_ERROR "Can't find isfinite()")
+endif()
+message(STATUS "Found isfinite: ${COIN_C_FINITE}")
+
+# ISNAN
+include(CheckCXXSourceCompiles)
+check_cxx_source_compiles(
+  "#include <cmath>\nint main(){return std::isnan(0);}"
+  HAVE_STD_ISNAN)
+include(CheckFunctionExists)
+include(CheckCXXSymbolExists)
+check_cxx_symbol_exists(isnan   "${TEST_INCLUDES}" HAVE_ISNAN)
+check_cxx_symbol_exists(_isnan  "${TEST_INCLUDES}" HAVE__ISNAN)
+check_cxx_symbol_exists(__isnan "${TEST_INCLUDES}" HAVE___ISNAN)
+if(HAVE_STD_ISNAN)
+  set(COIN_C_ISNAN "std::isnan")
+elseif(HAVE_ISNAN)
+  set(COIN_C_ISNAN "isnan")
+elseif(HAVE__ISNAN)
+  set(COIN_C_ISNAN "_isnan")
+elseif(HAVE___ISNAN)
+  set(COIN_C_ISNAN "__isnan")
+else()
+  message(FATAL_ERROR "Can't find isnan()")
+endif()
+message(STATUS "Found isnan: ${COIN_C_ISNAN}")
+
+# Basic type
+include(CheckTypeSize)
+check_type_size("int64_t" SIZEOF_INT64_T)
+check_type_size("long long" SIZEOF_LONG_LONG)
+check_type_size("long" SIZEOF_LONG)
+check_type_size("uint64_t" SIZEOF_UINT64_T)
+check_type_size("unsigned long long" SIZEOF_ULONG_LONG)
+check_type_size("unsigned long" SIZEOF_ULONG)
+check_type_size("intptr_t" SIZEOF_INTPTR_T)
+check_type_size("int *" SIZEOF_INT_P)
+
+if(SIZEOF_INT64_T EQUAL "8")
+  set(COIN_INT64_T "int64_t")
+elseif(SIZEOF_LONG EQUAL "8")
+  set(COIN_INT64_T "long")
+elseif(SIZEOF_LONG_LONG EQUAL "8")
+  set(COIN_INT64_T "long long")
+else()
+  message(FATAL_ERROR "Can't find suitable int64_t")
+endif()
+message(STATUS "Found int64_t: ${COIN_INT64_T}")
+
+if(SIZEOF_UINT64_T EQUAL "8")
+  set(COIN_UINT64_T "uint64_t")
+elseif(SIZEOF_ULONG EQUAL "8")
+  set(COIN_INT64_T "unsigned long")
+elseif(SIZEOF_ULONG_LONG EQUAL "8")
+  set(COIN_INT64_T "unsigned long long")
+else()
+  message(FATAL_ERROR "Can't find suitable uint64_t")
+endif()
+message(STATUS "Found uint64_t: ${COIN_UINT64_T}")
+
+if(SIZEOF_INTPTR_T)
+  set(COIN_INTPTR_T "intptr_t")
+elseif(SIZEOF_INT_P)
+  set(COIN_INTPTR_T "int *")
+else()
+  message(FATAL_ERROR "Can't find suitable intptr_t")
+endif()
+message(STATUS "Found intptr_t: ${COIN_INTPTR_T}")
+
diff --git a/cmake/Config.cmake.in b/cmake/Config.cmake.in
new file mode 100644
index 00000000..cdd574fe
--- /dev/null
+++ b/cmake/Config.cmake.in
@@ -0,0 +1,13 @@
+@PACKAGE_INIT@
+
+include(CMakeFindDependencyMacro)
+
+if(@ZLIB_FOUND@)
+  find_dependency(ZLIB)
+endif()
+
+if(@PTHREADS_FOUND@)
+  find_dependency(Threads)
+endif()
+
+include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
\ No newline at end of file
diff --git a/cmake/ParseAc.cmake b/cmake/ParseAc.cmake
new file mode 100644
index 00000000..f87a3f45
--- /dev/null
+++ b/cmake/ParseAc.cmake
@@ -0,0 +1,28 @@
+function(parse_ac VERSION_STRING VERSION_MAJOR VERSION_MINOR VERSION_PATCH)
+  file(READ "configure.ac" IN)
+  if(IN MATCHES "AC_INIT\\(.*trunk.*\\)")
+    message(WARNING "using trunk...")
+    set(MAJOR 999)
+    set(MINOR 0)
+    set(PATCH 0)
+  else()
+    # AC_INIT([CoinUtils],[major.minor.patch or trunk],[url or email])
+    string(REGEX MATCH
+      "AC_INIT\\([^,]+,\\[([0-9]+)\\.([0-9]+)(\\.([0-9]+))?\\],[^\\)]+\\)" AC_INIT ${IN})
+    message(STATUS "AC_INIT: ${AC_INIT}")
+    set(MAJOR ${CMAKE_MATCH_1})
+    set(MINOR ${CMAKE_MATCH_2})
+    if(CMAKE_MATCH_3)
+      set(PATCH ${CMAKE_MATCH_4})
+    else()
+      set(PATCH 0)
+    endif()
+  endif()
+  set(VERSION "${MAJOR}.${MINOR}.${PATCH}")
+
+  set(${VERSION_MAJOR} ${MAJOR} PARENT_SCOPE)
+  set(${VERSION_MINOR} ${MINOR} PARENT_SCOPE)
+  set(${VERSION_PATCH} ${PATCH} PARENT_SCOPE)
+  set(${VERSION_STRING} ${VERSION} PARENT_SCOPE)
+  message(STATUS "version: ${VERSION}")
+endfunction()
\ No newline at end of file
